buildscript {
    repositories {
        jcenter()
        maven { url 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'

group 'com.irtimaled'
version project.buildVersion + '-' + project.mcVersion
archivesBaseName = 'BBOutlineReloaded'

sourceCompatibility = 1.8
targetCompatibility = 1.8


minecraft {
    version = '1.12.2-14.23.5.2838'
    mappings = 'snapshot_20171003'
    runDir = 'run'
    makeObfSourceJar = false

    replace "@VERSION@", project.version
    replaceIn "com/irtimaled/bbor/forge/ForgeMod.java"

    replace "@MC_VERSION@", project.mcVersion
    replaceIn "com/irtimaled/bbor/forge/ForgeMod.java"
}

sourceSets {
    main
    debug {
        compileClasspath += main.compileClasspath
    }
}

processResources {
    filesMatching('mcmod.info') {
        expand([
            'version': project.version,
            'mcVersion': project.mcVersion,
            'formattedTime': new Date().format("yyyy-MM-dd'T'HH:mm:ss'-08:00'")
        ])
    }

    inputs.property "version", project.version
    inputs.property "mcVersion", project.mcVersion
}

jar {
    finalizedBy reobfJar

    classifier = 'forge'
}

artifacts {
    archives jar
}
