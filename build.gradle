buildscript {
    repositories {
        jcenter()
        maven { url 'https://www.dimdev.org/maven/' }
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.dimdev:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'java'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

def mcVersion = '1.13'
group 'com.irtimaled'
version "1.0.4-$mcVersion"
archivesBaseName = 'BBOutlineReloaded'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://www.dimdev.org/maven/' }
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') { transitive = false }
    implementation('net.minecraft:launchwrapper:1.12') { transitive = false }
    implementation 'org.ow2.asm:asm:6.2'
    implementation 'org.ow2.asm:asm-commons:6.2'
}

minecraft {
    version = mcVersion
    mappings = 'snapshot_20180908'
    runDir = 'run'
    tweakClass = 'com.irtimaled.bbor.launch.ClientTweaker'
    makeObfSourceJar = false

    replace "@VERSION@", project.version
    replaceIn "com/irtimaled/bbor/install/Main.java"

    replace "@MC_VERSION@", project.minecraft.version
    replaceIn "com/irtimaled/bbor/install/Main.java"
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.bbor.refmap.json'
}

sourceSets {
    main
    debug {
        compileClasspath += main.compileClasspath
    }
}

processResources {
    filesMatching('profile.json') {
        expand([
            'version': project.version,
            'mcVersion': project.minecraft.version,
            'tweakClass': project.minecraft.tweakClass,
            'formattedTime': new Date().format("yyyy-MM-dd'T'HH:mm:ss'-08:00'")
        ])
    }

    // Re-run this task when these change
    inputs.property "version", project.version
    inputs.property "mcVersion", project.minecraft.version
}

jar {
    finalizedBy reobfJar
    manifest.attributes(
            'Main-Class': 'com.irtimaled.bbor.install.Main'
    )
        
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'dummyThing'
    exclude 'LICENSE.txt'

    classifier = 'vanilla'
}

artifacts {
    archives jar
}
