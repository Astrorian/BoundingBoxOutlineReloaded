buildscript {
    repositories {
        jcenter()
        maven { url 'https://www.dimdev.org/maven/' }
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.dimdev:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

group 'com.irtimaled'
version '1.0.4-beta2'
archivesBaseName = 'BBOutlineReloaded'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://www.dimdev.org/maven/' }
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    implementation('net.minecraft:launchwrapper:1.12') { transitive = false }
    implementation 'org.ow2.asm:asm:6.2'
    implementation 'org.ow2.asm:asm-commons:6.2'
}

minecraft {
    version = '1.13'
    mappings = 'snapshot_20180908'
    runDir = 'run'
    tweakClass = 'com.irtimaled.bbor.launch.ClientTweaker'
    makeObfSourceJar = false

    replace "@VERSION@", project.version
    replaceIn "com/irtimaled/bbor/install/Main.java"

    replace "@MC_VERSION@", project.minecraft.version
    replaceIn "com/irtimaled/bbor/install/Main.java"
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.bbor.refmap.json'
}

sourceSets {
    main
    debug {
        compileClasspath += main.compileClasspath
    }
}

processResources {
    filesMatching('profile.json') {
        expand([
            'version': project.version,
            'mcVersion': project.minecraft.version,
            'tweakClass': project.minecraft.tweakClass,
            'formattedTime': new Date().format("yyyy-MM-dd'T'HH:mm:ss'-08:00'")
        ])
    }

    // Re-run this task when these change
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
}

jar {
    from sourceSets.main.output
    manifest.attributes(
            'Main-Class': 'com.irtimaled.bbor.install.Main'
    )
}

task devJar(type: Jar, dependsOn: classes) {
    classifier 'dev'
    from sourceSets.main.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

shadowJar {
    dependsOn 'reobfJar'
    from sourceSets.main.output
        
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    
    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }
    
    classifier = 'vanilla' + project.minecraft.version
}

reobf {
    jar {
        mappingType = 'NOTCH'
        classpath = sourceSets.main.compileClasspath
    }
    shadowJar {
        mappingType = 'NOTCH'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn {[
    'reobfJar',
    'reobfShadowJar'
]}

artifacts {
    archives shadowJar
    archives sourceJar
    archives devJar
}